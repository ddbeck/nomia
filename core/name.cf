entrypoints Name;

comment "/*" "*/" ;

-- TODO Need unicode identifier support before finalizing
-- TODO Should all of the uses of Identifier really be the same rules?
token Identifier letter (letter | digit | '_' | '-' | '\'')* ;

Tree. Name ::= Substitution ;
LetIn. Name ::= "let" [Declaration] "in" Substitution ;
Compose. Name ::= "compose" [Declaration] ;

Nullary. Substitution ::= NameRef ;
Multiary. Substitution ::= NameRef "(" [SubstitutionSpec] ")" ;

Bound. Declaration ::= Identifier "=" Substitution ;
Unbound. Declaration ::= Substitution ;
separator nonempty Declaration ";" ;

Formal. SubstitutionSpec ::= Identifier "=" OutputRef ;
Positional. SubstitutionSpec ::= OutputRef ;
separator nonempty SubstitutionSpec "," ;

Default. OutputRef ::= Substitution ;
Specified. OutputRef ::= Substitution "." Identifier;

Atomic. NameRef ::= AtomicName ;
Variable. NameRef ::= "$" Identifier ;
Resolved. NameRef ::= "@" Identifier ;
Nested. NameRef ::= "(" Name ")";

Qualified. AtomicName ::= NamespaceId ":" ParameterizedId ;
Unqualified. AtomicName ::= ParameterizedId ;

Builtin. NamespaceId ::= ParameterizedId ;
Named. NamespaceId ::= "[" OutputRef "]" ;

UnParameterized. ParameterizedId ::= Identifier ;
Parameterized. ParameterizedId ::= Identifier "?" [Param] ;

-- TODO Support multiple types?
Quoted. ParamValue ::= String ;
-- TODO We should support more robust unquoted strings, but annoying to get the lexer to behave
Unquoted. ParamValue ::= Identifier ;
Parameter. Param ::= Identifier "=" ParamValue ;
separator nonempty Param "&" ;

-- TODO Name metadata
